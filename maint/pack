#!/usr/bin/env perl
use strict;
use warnings;
use File::Temp ();
use File::Find;
use File::Path;
use File::Basename;
use Module::CoreList;
use autodie qw(:all);

my ($exclude) = map { qr{$_} } join '|', qw(
    ^Sub::Name$
    ^Class::XSAccessor
);

my @modules;
find({no_chdir => 1, wanted => sub {
    -f or return;
    s/\.pm$// or return;
    s{^lib/}{};
    s{/}{::}g;
    push @modules, $_;
}}, 'lib');

{
    local $ENV{PERL5LIB} = "lib:" . ($ENV{PERL5LIB}||'');
    system 'fatpack', 'trace', (map { "--use=$_" } @modules), 'bin/bcssh';
}
my @trace = do {
    open my $fh, '<', 'fatpacker.trace';
    <$fh>;
};
chomp @trace;
@trace = grep {
    my $mod = $_;
    $mod =~ s/\.pm$//;
    $mod =~ s{/}{::}g;
    ! ( $Module::CoreList::version{5.008008}{$mod} || $mod =~ $exclude || grep { $mod eq $_ } @modules )
} @trace;

my @packlists = `fatpack packlists-for @trace`;
chomp @packlists;

rmtree('fatlib');
system 'fatpack', 'tree', @packlists;


my $content = "#!/usr/bin/env perl\n";
$content .= `fatpack file`;
$content .= do {
    open my $fh, '<', 'bin/bcssh';
    <$fh>
};

open my $fh, '>', 'bcssh';
print { $fh } $content;
close $fh;
chmod 0777 & ~umask, 'bcssh';
print "wrote bcssh\n";

rmtree('fatlib');
unlink 'fatpacker.trace';
